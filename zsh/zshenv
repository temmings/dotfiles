# -*- coding: utf-8 -*-
#zmodload zsh/zprof && zprof

### functions {{{
function source_if_exist {
  [[ -f "$1" ]] && source "$1"
}

function get_value_of_key() {
    local file="$1"
    local key=$2
    local delimiter=$3
    if [ -n "$file" -a -f "$file" -a -n "$key" -a -n "$delimiter" ]; then
        grep -h "${key}${delimiter}" $file | awk -F"$delimiter" '{print $2;}'
    fi
}

function is_executable() {
    (($+commands[$1]))
}

function get_executable() {
    is_executable "$1" && echo "$1" || return
}

function get_executable_first() {
    while [[ -n "$1" ]]; do
        get_executable "$1" && return
        shift
    done
    return 1
}

function is_exist_func() {
    (($+functions[$1]))
}

function abs_path() {
    (cd -P -- "$(dirname -- "$1")" && echo "$(pwd -P)/$(basename -- "$1")")
}

function home_or_basename() {
    case "$1" in
        $HOME) echo '~';;
        *) echo $(basename $1) ;;
    esac
}

function get_ssh_client_ip() {
    echo $SSH_CLIENT | awk '{print $1}'
}
# }}}

local my_zsh_function_path=$HOME/.zsh/functions
fpath=($my_zsh_function_path $fpath)

[[ "$OSTYPE" = "cygwin" ]] && unset PATH
##  N: NULL_GLOBオプションを設定。globがマッチしなかったり存在しないパスを無視する。
##  -: シンボリックリンク先のパスを評価。
##  /: ディレクトリのみ残す。
## ^W: reject world-writable directories
path=(
    ~/bin(N-/)
    ~/.local/bin(N-/)
    ~/Dropbox/bin(N-/)
    ~/node_modules/.bin(N-/)
    ~/.cabal/bin(N-/)
    ~/.go/bin(N-/)
    ### pear / pecl $ php -d detect_unicode=0 =(curl -fsSL http://pear.php.net/go-pear.phar)
    ~/pear/bin(N-/)
    ### cpanminus
    ~/perl5/bin(N-/)
    /usr/local/heroku/bin(N-/)
    /usr/local/bin(N-/)
    /usr/local/sbin(N-/)
    /sbin(N-/)
    /bin(N-/)
    /usr/sbin(N-/)
    /usr/bin(N-/)
    ${^path}(N-/^W)
    /usr/games(N-/)
    /usr/X11/bin(N-/)
    /usr/X11R6/bin(N-/)
    ### NetBSD packages
    /usr/pkg/bin(N-/)
    /usr/pkg/sbin(N-/)
    ### qmail
    /var/qmail/bin(N-/)
)
typeset -U path PATH

export EDITOR=$(get_executable_first "vim" "vi")
export PAGER="less"

export LANG="en_US.UTF-8"
export LC_COLLATE=$LANG
export LC_CTYPE=$LANG
export LC_MESSAGES=$LANG
export LC_MONETARY=$LANG
export LC_NUMERIC=$LANG
export LC_TIME=$LANG
#export LC_ALL="en_US.UTF-8"
export TZ=JST-9
export BLOCKSIZE=M

export LYNX_CFG=~/.lynxrc
export PERL_BADLANG=0
PERL_CPANM_LOCAL_LIB="$HOME/perl5"
export PERL_CPANM_OPT="--local-lib=$PERL_CPANM_LOCAL_LIB"
export PERL5LIB="$PERL_CPANM_LOCAL_LIB/lib/perl5:$PERL5LIB"
#export PIP_REQUIRE_VIRTUALENV=true
export GOPATH=$HOME/.go

export WWW_HOME=http://www.google.co.jp/
export HTTP_HOME=$WWW_HOME

export AWS_DEFAULT_REGION="ap-northeast-1"
export AWS_DEFAULT_PROFILE="default"

#export CVSROOT=/home/ncvs/tsc
#export PDX_PATH=~acid/local/lib/pdx

# for OpenCV
#typeset -xT PYTHONPATH python_path
#typeset -U python_path
#python_path=(/usr/local/lib/python2.7/site-packages $python_path)

### os specific
case "$OSTYPE" in
    cygwin*) source_if_exist ~/.zsh/zshenv.cygwin ;;
    darwin*) source_if_exist ~/.zsh/zshenv.darwin ;;
    freebsd*) source_if_exist ~/.zsh/zshenv.freebsd ;;
    linux*) source_if_exist ~/.zsh/zshenv.linux ;;
esac

typeset -U path PATH

source_if_exist ~/.zshenv.local
