# -*- coding: utf-8 -*-
# vim:ts=4 foldmethod=marker

### on exec
## 1: /etc/zshenv
## 2: ~/.zshenv
## if login shell; then
##   3: /etc/zprofile
##   4: ~/.zprofile
## fi
## if interactive shell; then
## 5: /etc/zshrc
## 6: ~/.zshrc
## fi
## if login shell; then
## 7: /etc/zlogin
## 8: ~/.zlogin
## fi

### on logout
## 1: ~/.zlogout
## 2: /etc/zlogout

### functions {{{

### http://qiita.com/mollifier/items/160a13a95e9627a55750
function 256colortest() {
    local code
    for code in {000..255}; echo -n -e "\e[38;05;${code}m $code"
}

function set_screen_caption() {
    # set program name to screen caption
    [ -n "$STY" ] && echo -ne "\ek$1\e\\"
}

function mkcdir() {
    mkdir "$@" || return $?

    local arg got_end_option=0
    for arg in "$@"; do
        case "$arg" in
            --)
                got_end_option=1
                continue
                ;;
            -*)
                ((1 == $got_end_option)) && break
                continue
                ;;
            *)
                break
                ;;
        esac
    done
    cd -- "$arg"
}

### base: https://github.com/mooz/percol#zsh-history-search
local cmd_peco=$(get_executable_first peco percol)
if [[ ${cmd_peco} ]]; then
    function select_history() {
        local tac
        tac=$(get_executable_first "gtac" "tac") || tac="tail -r"
        BUFFER=$(fc -l -n 1 | eval $tac | $cmd_peco --query "$LBUFFER" | sed 's/\\n/\n/g')
        CURSOR=$#BUFFER
        zle clear-screen
    }

    zle -N select_history
fi

function configure_bootstrap() {
    #if which glibtoolize &>/dev/null; then
    #    glibtoolize --force
    #else
    #    libtoolize --force
    #fi
    aclocal && autoconf && autoheader && automake --add-missing --foreign
    #shtoolize all
}

function configure_for_home_local() {
    ./configure --prefix=$HOME/.local "$@"
}

function prepare_boot2docker() {
    boot2docker init
    boot2docker start
    $(boot2docker shellinit)
}

function prepare_anyenv() {
    local anyenv_dir=$HOME/.anyenv
    [[ -d $anyenv_dir ]] || git clone https://github.com/riywo/anyenv $anyenv_dir
    if [[ ! -d $anyenv_dir/plugins/anyenv-update ]]; then
        mkdir -p $anyenv_dir/plugins
        git clone https://github.com/znz/anyenv-update.git $anyenv_dir/plugins/anyenv-update
    fi
    export PATH="$anyenv_dir/bin:$PATH"
    eval "$(anyenv init -)"
}

function cdwork() {
    local yyyy=$(date '+%Y')
    local mmdd=$(date '+%m%d')
    mkcdir -p ~/${yyyy}/${mmdd}
}

function ssh-concat-config() {
    local d=$HOME/.ssh
    [ -f $d/config ] && mv $d/config{,.bak}
    cat $d/conf.d/*.conf > $d/config
}
# }}}

# show user@host and random color.
PROMPT="%{[$[32 + $RANDOM % 5]m%}%n@%m%{[m%} %(#.#.$) "

# show current path.
RPROMPT="%{[36m%}%~%{[m%}"

# ignore ['?', '&', '/'] in word.
WORDCHARS='*_-.[]~=;!#$%^(){}<>'

HISTFILE="$HOME/.zsh-history"
HISTSIZE=100000000
SAVEHIST=100000000

# suppress the logout ^D.
IGNOREEOF=2

USE_TMUX=1

### alias {{{
alias ll="ls -l"
alias lf="ls -F"
alias la="ls -A"
alias clear2="echo -e '\026\033c'"
alias ssh="ssh-concat-config; ssh"
alias scp="ssh-concat-config; scp"
alias sftp="ssh-concat-config; sftp"
alias svstatall="sudo svstat /var/service/*"
if is_executable w3mman; then
    alias man="w3mman"
    alias run-help="w3mman"
fi
if is_executable vim; then
    alias vi="vim"
fi

# command on tmux panel
alias t="tmux split-window -v"
alias tb="tmux split-window -bdv"
alias th="tmux split-window -h"
alias tp="tmux new-window"

# see: http://qiita.com/itkrt2y/items/0671d1f48e66f21241e2
alias cdg='(){ cd $(ghq root)/$(ghq list | peco --query "$*") }'
alias gh='(){ hub browse $(ghq list | peco --query "$*" | cut -d "/" -f 2,3) }'

alias vinarise="vim -c Vinarise"

alias git-init="git init && git add -A && git commit -m 'Initialize repository'"

alias grep_nm_func_names="grep ' T _' | sed 's/.* T _//'"

alias show_my_ipv4="telnet ipv4.test-ipv6.com 79"
alias show_my_ipv6="telnet ipv6.test-ipv6.com 79"
alias show_my_external_ip="curl 'http://whatismyip.akamai.com'"

alias show_ec2_local_ipv4="curl -s 'http://169.254.169.254/latest/meta-data/local-ipv4'"
alias show_ec2_public_ipv4="curl -s 'http://169.254.169.254/latest/meta-data/public-ipv4'"

alias ssl-x509-detail="openssl x509 -text -noout -in"
alias ssl-rsa-detail="openssl rsa -text -noout -in"
alias ssl-csr-detail="openssl req -text -noout -in"
alias ssl-pfx-detail='(){ openssl pkcs12 -clcerts -nokeys -in $1 | openssl x509 -text -noout }'
alias ssl-show-https='(){ openssl s_client -connect $1:443 -showcerts }'
alias urlencode="nkf -WwMQ | tr = %"
alias urldecode="nkf -w --url-input"

alias sum-col='(){ awk "{s+=\$${1:-1}}END{print s}" }'

alias -s exe="wine"
alias -s htm="w3m"
alias -s html="w3m"
alias -s shtml="w3m"
alias -s phar="php -f"
alias -s php="php -f"
alias -s pl="perl"
alias -s py="python"
alias -s rb="ruby"
# shbang ã« -eu ã¨ã‹æŒ‡å®šã•ã‚Œã¦ã¦ã‚‚åŠ¹ã‹ãªããªã‚‹ã®ã§ã‚„ã‚ãŸ
#alias -s sh="sh"
#alias -s zsh="zsh"
# }}}

if [[ x"$OSTYPE" != x"cygwin" ]]; then
    prepare_anyenv
    is_executable direnv && eval "$(direnv hook zsh)"
fi

### virtualenvwrapper for python virtualenv
#if hash virtualenvwrapper.sh 2>/dev/null; then
#    WORKON_HOME="$HOME/.virtualenvs"
#    source $(which virtualenvwrapper.sh)
#fi

source_if_exist ~/.opam/opam-init/init.zsh > /dev/null 2> /dev/null || true

binary_extension_patterns='*.(o|obj|a|so|dylib|lib|dll|com|exe|pyc|elc|hi|class|pdb|cmi|cmo)'
zstyle ':completion:*:*:'$EDITOR':*:*files' ignored-patterns $binary_extension_patterns
zstyle ':completion:*:*:'$PAGER':*:*files' ignored-patterns $binary_extension_patterns
zstyle ':completion:*:*:'view':*:*files' ignored-patterns $binary_extension_patterns

zstyle ':completion:*' group-name ''
zstyle ':completion:*:descriptions' format '%F{yellow}Completing %B%d%b%f'

### os specific
case "$OSTYPE" in
    cygwin*) source_if_exist ~/.zsh/zshrc.cygwin ;;
    darwin*) source_if_exist ~/.zsh/zshrc.darwin ;;
    freebsd*) source_if_exist ~/.zsh/zshrc.freebsd ;;
    linux*) source_if_exist ~/.zsh/zshrc.linux ;;
esac

typeset -U path PATH

source_if_exist ~/.zsh/zshrc.zplug

setopt auto_cd
setopt auto_pushd
setopt pushd_ignore_dups
setopt auto_menu
setopt print_eight_bit
setopt ignore_eof
setopt no_correct
setopt always_last_prompt

setopt extended_glob
setopt prompt_subst
setopt magic_equal_subst
setopt auto_remove_slash
setopt brace_ccl
setopt no_clobber
setopt no_glob_dots

setopt hist_ignore_space
setopt hist_ignore_all_dups
#setopt hist_ignore_dups
setopt extended_history
setopt share_history

unsetopt all_export
unsetopt bg_nice
unsetopt no_match

bindkey -e
is_exist_func select_history && bindkey '^R' select_history

#autoload -U compinit && compinit -C -u
source_if_exist /usr/local/share/zsh/site-functions/_aws

source_if_exist ~/.zshrc.local

if (which zprof > /dev/null); then
    zprof | less
fi
